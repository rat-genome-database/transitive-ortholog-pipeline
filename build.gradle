// create a runnable jar with jar dependencies stored in lib subdirectory
tasks.whenTaskAdded { task ->
    ['startScripts', 'distTar'].each { String skipTaskName ->
        if (task.name.contains(skipTaskName)) {
            task.enabled = false
        }
    }
}
apply plugin: 'java'
apply plugin: 'application'

// JDK 8
sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = 'edu.mcw.rgd.TransitiveOrthologPipeline.Manager'
String myAppName = 'TransitiveOrthologPipeline'

repositories {
    mavenCentral()
    maven {
        url 'https://packages.atlassian.com/maven-3rdparty'
    }
}

dependencies {
    compile 'commons-dbcp:commons-dbcp:1.4'
    compile 'commons-net:commons-net:3.6'
    compile 'log4j:log4j:1.2.17'
    compile 'org.apache.httpcomponents:httpclient:4.5.6'
    compile 'org.springframework:spring-beans:5.1.4.RELEASE'
    compile 'org.springframework:spring-jdbc:5.1.4.RELEASE'
    compile 'com.oracle:ojdbc6:11.2.0.4.0-atlassian-hosted'

    compile fileTree(dir: 'lib', include: '*.jar')
}

jar {
    baseName = myAppName
    manifest {
        attributes(
            'Built-By': System.getProperty('user.name'),
            'Built-Date': new Date(),
            'Built-JDK': System.getProperty('java.version'),
            'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
            'Main-Class': mainClassName
        )
    }
}

distributions {
    main {
        baseName = myAppName
    }
}

task createDistro(type: Copy) {
    def zipFile = file('build/distributions/'+myAppName+'.zip')
    def outputDir = file("build/install")

    from zipTree(zipFile)
    into outputDir
}
createDistro.dependsOn assembleDist
