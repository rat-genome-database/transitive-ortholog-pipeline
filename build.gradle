// create a runnable jar with jar dependencies stored in lib subdirectory
tasks.whenTaskAdded { task ->
    ['startScripts', 'distTar'].each { String skipTaskName ->
        if (task.name.contains(skipTaskName)) {
            task.enabled = false
        }
    }
}
apply plugin: 'java'
apply plugin: 'application'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}
mainClassName = 'edu.mcw.rgd.TransitiveOrthologPipeline.Manager'
String myAppName = 'transitive-ortholog-pipeline'

project.archivesBaseName = myAppName

repositories {
    mavenCentral()
}

dependencies {
    implementation ('org.apache.commons:commons-dbcp2:2.12.0') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    implementation 'commons-net:commons-net:3.10.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.23.0'
    implementation ('org.apache.httpcomponents:httpclient:4.5.14') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    implementation 'com.oracle.database.jdbc:ojdbc11:23.7.0.25.01'
    implementation 'org.springframework:spring-jdbc:6.2.3'

    implementation fileTree(dir: 'lib', include: '*.jar')
}

jar {
    manifest {
        attributes(
            'Built-By': System.getProperty('user.name'),
            'Built-Date': new Date(),
            'Built-JDK': System.getProperty('java.version'),
            'Class-Path': configurations.runtimeClasspath.collect { it.getName() }.join(' '),
            'Main-Class': mainClassName
        )
    }
}

distributions {
    main {
        distributionBaseName = myAppName
    }
}

task createDistro(type: Copy) {
    def zipFile = file('build/distributions/'+myAppName+'.zip')
    def outputDir = file("build/install")

    from zipTree(zipFile)
    into outputDir
}
createDistro.dependsOn assembleDist
